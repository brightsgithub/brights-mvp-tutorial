buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        // https://github.com/evant/gradle-retrolambda
        // A gradle plugin for getting java lambda support in java 6, 7 and android
        classpath 'me.tatarka:gradle-retrolambda:3.3.0-beta4'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda' // so we can use Lambdas

android {
    // You can do testing on a different build variant; but only on one! The default is debug.
    // Because we are using a different classes from our normal debug variant to test we use
    // debugForTesting. debug will function as normal i.e. it will call the real server when
    // the app is launched etc
    testBuildType "debugForTesting"

    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "com.examples.bright.tutorial"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debugForTesting {
            debuggable = true
            minifyEnabled false
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        // To avoid: DuplicateFileException: Duplicate files copied in APK META-INF/rxjava.properties
        // This exclude must be kept at in this module gradle file rather than the data layer
        // module. This is due to the build order?
        exclude 'META-INF/rxjava.properties'

        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // https://stackoverflow.com/questions/28476507/using-espresso-to-click-view-inside-recyclerview-item
    // Kept getting lib conflicts, the below is now resolved
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.0') {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude module: 'recyclerview-v7'
    }

    // This project depends on the domain layer
    //compile project(':domainlayer')

    // When we change one variant, they will all be in sync. See publishNonDefault true above
    debugCompile project(path: ':domainlayer', configuration: "debug")
    releaseCompile project(path: ':domainlayer', configuration: "release")
    debugForTestingCompile project(path: ':domainlayer', configuration: "debugForTesting")

    // Android
     compile 'com.android.support:cardview-v7:25.3.1'
     compile 'com.android.support:appcompat-v7:25.3.1'
     compile 'com.android.support:recyclerview-v7:25.3.1'
     compile 'com.android.support.constraint:constraint-layout:1.0.2'

    testCompile 'junit:junit:4.12'
    androidTestCompile project(':testutils')

    // Dagger for DI
    compile "com.google.dagger:dagger:2.11"
    annotationProcessor "com.google.dagger:dagger-compiler:2.11"

    // Circular Progressbar libary
    compile 'com.github.castorflex.smoothprogressbar:library-circular:1.2.0'

    //http://jakewharton.github.io/butterknife/
    compile 'com.jakewharton:butterknife:8.6.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.6.0'

    // Picasso
    compile 'com.squareup.picasso:picasso:2.5.2'

    // Mockito runs on the local JVM not the device.
    testCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'com.crittercism.dexmaker:dexmaker:1.4'
    androidTestCompile 'com.crittercism.dexmaker:dexmaker-mockito:1.4'

    // 64k Method limit (https://developer.android.com/studio/build/multidex.html)
    compile 'com.android.support:multidex:1.0.1'
}
